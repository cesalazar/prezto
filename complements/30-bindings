#
# KEYBINGINS
#

# Jump words with C-arrows
bindkey -M vicmd "5C" vi-forward-blank-word
bindkey -M vicmd "5D" vi-backward-blank-word
bindkey -M viins "^[[1;5C" vi-forward-blank-word
bindkey -M viins "^[[1;5D" vi-backward-blank-word
bindkey -M vicmd "^[l" vi-forward-blank-word
bindkey -M vicmd "^[h" vi-backward-blank-word
bindkey -M viins "^[h" vi-backward-char
bindkey -M viins "^[l" vi-forward-char
bindkey -M vicmd 'V' edit-command-line
bindkey -M viins '^E' transpose-chars
bindkey -M vicmd '^E' transpose-chars
bindkey -M viins '^X^X' vi-kill-line
# Delete backwards with C-delete (^H)
bindkey -M viins '^H' backward-kill-word
# Conflicts with vim Yankring
bindkey -r "^Y"-"^Z"
# Handle the history search only if fzf is not available
[[ -f ~/.fzf.zsh ]] || bindkey -M viins '^R' history-incremental-search-backward

# Redraw the prompt without creating a newline
function redraw-prompt() {
  for precmd in ${precmd_functions}; do
    ${precmd}
  done
  zle reset-prompt
}
zle -N redraw-prompt

# Change exported AWS profile
function change-aws-profile() {
  local aws_credentials
  aws_credentials=~/.aws/credentials

  if [[ -f "${aws_credentials}" ]]; then
    AWS_PROFILE=$(cat "${aws_credentials}" | grep "\[" | grep -v 'onelogin' \
      | tr -d "[]" | fzf --height 50%)
    export AWS_PROFILE
    zle && zle redraw-prompt
  fi
}
zle -N change-aws-profile
bindkey -M viins '^[a' change-aws-profile

# Use fzf to pick files and open them in $EDITOR
function fzf-open-editor() {
  local files

  if [[ $(command -v fd) ]]; then
    files=$(fd -H -tf -E .git | fzf)
  else
    files=$(find . -type f | grep -Ev "(node_modules|.git)" | fzf)
  fi

  # The prompt redrawing is funky, trying to figure out what works
  [[ ${files} ]] && printf "%s" "${files}" | xargs -o "${EDITOR:-vim}" \
    && printf "%s\n" "${files}" "" "" && zle redraw-prompt
}
zle -N fzf-open-editor
bindkey -M viins '^[e' fzf-open-editor
bindkey -M viins '^[f' fzf-open-editor

# Use fzf to pick a npm script to execute
function fzf-npm-run() {
  [[ ! $(command -v jq) || ! -f package.json ]] && return

  local cmd
  [[ -f yarn.lock ]] && cmd=yarn || cmd=npm

  local script
  script=$(jq -C --indent 1 '.scripts' package.json \
    | \grep -Ev "(\{|\})" \
    | tr -d ',' \
    | fzf --ansi \
    | awk '{print $1}' \
    | tr -d '"')

  [[ ${script} ]] && ${cmd} run "${script%:}" && printf "\n\n"

  zle redraw-prompt
}
zle -N fzf-npm-run
bindkey -M viins '^[n' fzf-npm-run

# Use fzf to pick a node-related process to kill
function fzf-node-kill() {
  local pids=
  pids=$(
    ps -eo pid,user,args --sort user \
      | grep -v grep \
      | grep -E "(pnpm|npm|react|yarn|bun)" \
      | fzf \
      | awk '{print $1}'
  )
  [ ! -z ${pids} ] && echo ${pids} | xargs kill
  zle && zle redraw-prompt
}
zle -N fzf-node-kill
bindkey -M viins '^[N' fzf-node-kill

# Atuin (history manager)
if [[ $(command -v atuin) ]]; then
  export ATUIN_NOBIND="true"
  eval "$(atuin init zsh)"

  bindkey '^g' _atuin_search_widget
  bindkey -M vicmd '/' _atuin_search_widget
fi

# vim: ft=zsh fdm=manual tabstop=2 softtabstop=2 shiftwidth=2 expandtab:
