#
# KEYBINGINS
#

# Jump words with C-arrows
bindkey -M vicmd "5C" vi-forward-blank-word
bindkey -M vicmd "5D" vi-backward-blank-word
bindkey -M viins "^[[1;5C" vi-forward-blank-word
bindkey -M viins "^[[1;5D" vi-backward-blank-word
bindkey -M vicmd "^[l" vi-forward-blank-word
bindkey -M vicmd "^[h" vi-backward-blank-word
bindkey -M viins "^[h" vi-backward-char
bindkey -M viins "^[l" vi-forward-char
bindkey -M vicmd 'V' edit-command-line
bindkey -M viins '^E' transpose-chars
bindkey -M vicmd '^E' transpose-chars
bindkey -M viins '^X^X' vi-kill-line
# Delete backwards with C-delete (^H)
bindkey -M viins '^H' backward-kill-word
# Conflicts with vim Yankring
bindkey -r "^Y"-"^Z"
# Handle the history search only if fzf is not available
[[ -f ~/.fzf.zsh ]] || bindkey -M viins '^R' history-incremental-search-backward

# Redraw the prompt without creating a newline
function redraw-prompt() {
  local precmd
  for precmd in $precmd_functions; do
    $precmd
  done
  zle reset-prompt
}
zle -N redraw-prompt

# Change exported AWS profile
function change-aws-profile() {
  local aws_credentials
  aws_credentials=~/.aws/credentials
  if [[ -f "${aws_credentials}" ]]; then
    AWS_PROFILE=$(cat "${aws_credentials}" | grep "\[" | grep -v 'onelogin' \
      | tr -d "[]" | fzf --height 50%)
    export AWS_PROFILE
    zle && zle redraw-prompt
  fi
}
zle -N change-aws-profile
bindkey -M viins '^[a' change-aws-profile

# Use fzf to pick files and open them in $EDITOR
function fzf-open-editor() {
  local files
  if [[ $(command -v fd) ]]; then
    files=$(fd -t f | fzf)
  else
    files=$(find . -type f | fzf)
  fi

  # The prompt redrawing is funky, trying to figure out what works
  [[ ${files} ]] && echo "${files}" | xargs -o "${EDITOR:-vim}" \
    && printf "%s\n" " " " " && zle && zle redraw-prompt
}
zle -N fzf-open-editor
bindkey -M viins '^[e' fzf-open-editor

# Use fzf to pick a npm script to execute
function fzf-npm-run() {
  [[ ! -f package.json ]] && return

  jq -C --indent 1 '.scripts' package.json \
    | grep -v "{" \
    | grep -v "}" \
    | tr -d ',' \
    | sort \
    | fzf --ansi \
    | awk '{print $1}' \
    | tr -d '":,' \
    | xargs -I{} npm run {}
}
zle -N fzf-npm-run
bindkey -M viins '^[n' fzf-npm-run

# vim: ft=zsh fdm=manual tabstop=2 softtabstop=2 shiftwidth=2 expandtab:
